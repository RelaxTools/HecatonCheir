VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "TextWriter"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
'-----------------------------------------------------------------------------------------------------
'
' [Hidennotare] v1
'
' Copyright (c) 2019 Yasuhiro Watanabe
' https://github.com/RelaxTools/Hidennotare
' author:relaxtools@opensquare.net
'
' The MIT License (MIT)
'
' Permission is hereby granted, free of charge, to any person obtaining a copy
' of this software and associated documentation files (the "Software"), to deal
' in the Software without restriction, including without limitation the rights
' to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
' copies of the Software, and to permit persons to whom the Software is
' furnished to do so, subject to the following conditions:
'
' The above copyright notice and this permission notice shall be included in all
' copies or substantial portions of the Software.
'
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
' OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
' SOFTWARE.
'
'-----------------------------------------------------------------------------------------------------
' 依存モジュール
'   IWriter.cls
'-----------------------------------------------------------------------------------------------------
' 2018-12-06 Ver.1.0.0 新規作成 テキストWriteクラス
'-----------------------------------------------------------------------------------------------------
Option Explicit
Implements IConstructor
Implements IWriter

Private fp As Integer
Private mOpen As Boolean

'プロパティ保持
Private mFileName As String
Private mNewLineCode As NewLineCodeConstants
Private mEncode As EncodeConstants
Private mMode As OpenModeConstants
Private mBom As Boolean

Private mFirst As Boolean
'--------------------------------------------------------------
' コンストラクタ
'--------------------------------------------------------------
' Arg1 - FileName
' Arg2 - NewLineCode : NewLineCodeConstants.CRLF/LF
' Arg3 - Encode      : EncodeConstants.CP932/UTF8/UTF16LE
' Arg4 - Mode        : OpenModeConstants.Output/Append
' Arg5 - Bom         : True/False
'--------------------------------------------------------------
Private Function IConstructor_Instancing(Args As Collection) As Object

    If Args.Count = 0 Then
        Exit Function
    End If
    
    'Default
    mNewLineCode = NewLineCodeConstants.CRLF
    mEncode = EncodeConstants.CP932
    mMode = OpenModeConstants.Output
    mBom = False
    
    If Args.Count >= 1 Then
        mFileName = Args(1)
    End If
    
    If Args.Count >= 2 Then
        mNewLineCode = Args(2)
    End If
    
    If Args.Count >= 3 Then
        mEncode = Args(3)
    End If
    
    If Args.Count >= 4 Then
        mMode = Args(4)
    End If
    
    If Args.Count >= 5 Then
        mBom = Args(5)
    End If
    
    Set IConstructor_Instancing = Me


    Dim strFolder As String
    
    'フォルダの存在チェック
    strFolder = FileIO.GetParentFolderName(mFileName)
    
    If Not FileIO.FolderExists(strFolder) Then
        Err.Raise 512 + 1, TypeName(Me) & ".IConstructor_Instancing", "not exists folder. (" & strFolder & ")"
    End If

    Call fileOpen

    mFirst = True

End Function

Private Sub IWriter_Append(ByVal Args As Variant)
    
    Dim strLine As String
    Dim bytBuf() As Byte
    Dim bytBom() As Byte
    
    '初回BOMの書き込みを行う。
    If mBom And mFirst And mMode = OpenModeConstants.Output Then
        'エンコード
        Select Case mEncode
            Case EncodeConstants.UTF8
                
                ReDim bytBom(0 To 2)
            
                bytBom(0) = &HEF
                bytBom(1) = &HBB
                bytBom(2) = &HBF
            
                Put #fp, , bytBom
            
            Case EncodeConstants.UTF16LE
                
                ReDim bytBom(0 To 1)
            
                bytBom(0) = &HFF
                bytBom(1) = &HFE
        
                Put #fp, , bytBom
        
        End Select
        mFirst = False
    End If

    '改行
    Select Case mNewLineCode
        Case NewLineCodeConstants.CRLF
            strLine = Args & vbCrLf
        Case NewLineCodeConstants.LF
            strLine = Args & vbLf
        Case NewLineCodeConstants.None
            strLine = Args
    End Select
        
    'エンコード
    Select Case mEncode
        Case EncodeConstants.CP932 'SJIS
            bytBuf = VBA.StrConv(strLine, vbFromUnicode)
        Case EncodeConstants.UTF8
            bytBuf = Convert.ToUTF8(strLine)
        Case EncodeConstants.UTF16LE
            bytBuf = strLine
    End Select

    Put #fp, , bytBuf

End Sub

Private Sub IWriter_Flush()
    
    If mOpen = False Then Exit Sub

    On Error Resume Next
    
    If mOpen Then
        Close fp
    End If
    
    fp = FreeFile
    Open mFileName For Binary As fp
    mOpen = True

    Seek #fp, VBA.LOF(fp) + 1
    
End Sub

Private Sub Class_Terminate()

    On Error Resume Next

    '開いている場合にはクローズ
    If mOpen Then
        Close fp
    End If

End Sub

Private Sub fileOpen()
    
    Select Case mMode
        Case OpenModeConstants.Output
            
            fp = FreeFile()
            Open mFileName For Output As fp
            Close fp
        
            fp = FreeFile
            Open mFileName For Binary As fp
            mOpen = True
        
        Case OpenModeConstants.AppendFile
            
            fp = FreeFile
            Open mFileName For Binary As fp
            mOpen = True
        
            Seek #fp, VBA.LOF(fp) + 1
    
    End Select

End Sub

